prefix as: <http://www.w3.org/ns/activitystreams#>
prefix dc: <http://purl.org/dc/elements/1.1/>
prefix dct: <http://purl.org/dc/terms/>
prefix dctypes: <http://purl.org/dc/dcmitype/>
prefix exif: <http://www.w3.org/2003/12/exif/ns#>
prefix foaf: <http://xmlns.com/foaf/0.1/>
prefix haCarId: <https://data.hetarchief.be/id/carrier-type/>
prefix iiif_prezi: <http://iiif.io/api/presentation/3#>
prefix iiif_image_3: <http://iiif.io/api/image/3#>
prefix mf: <https://api.meemoo-int.triply.cc/queries/meemoo/~/iiif-glassplate-manifest/>
prefix oa: <http://www.w3.org/ns/oa#>
prefix org: <http://www.w3.org/ns/org#> 
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix prov: <http://www.w3.org/ns/prov#>
prefix schema: <https://schema.org/>
prefix haLic: <https://data.hetarchief.be/id/license/>
prefix mh: <https://data.hetarchief.be/ns/mediahaven/>
prefix haObj: <https://data.hetarchief.be/ns/object/>
prefix haOrg: <https://data.hetarchief.be/ns/organization/>
prefix rel: <http://id.loc.gov/vocabulary/preservation/relationshipSubType/>
prefix premis: <http://www.loc.gov/premis/rdf/v3/>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>


CONSTRUCT {
  ?manifestIri a iiif_prezi:Manifest ;
               rdfs:label ?titleNl ;
               iiif_prezi:behavior iiif_prezi:individualsHint ;
               dct:rights <http://creativecommons.org/licenses/by/4.0/deed.nl> ;
               schema:provider ?cp ;
               iiif_prezi:metadataEntries mf:type, mf:dateCreated, mf:pid ;
               iiif_prezi:requiredStatement mf:requiredStatement ;
               iiif_prezi:thumbnail ?browseThumbnailIri ;
               as:items ?canvasIri .

  ?canvasIri a iiif_prezi:Canvas ;
             exif:width ?fileWidth;
             exif:height ?fileHeight;
             iiif_prezi:thumbnail ?browseThumbnailIri ;
             rdfs:label ?titleNl ;
             as:items ?annotationPageIri .

  ?annotationPageIri a as:OrderedCollectionPage ;
                     as:items ?annotationIri .

  ?annotationIri a oa:Annotation ;
                oa:motivatedBy iiif_prezi:painting ;
                oa:hasTarget ?canvasIri ;
                oa:hasBody ?bodyIri .

  ?bodyIri a dctypes:StillImage ;
            dc:format "image/jpeg" ;
            schema:potentialAction ?serviceIri .

  ?serviceIri  a iiif_image_3:ImageService ;
               dct:conformsTo <http://iiif.io/api/image/3/level2.json> .

  

  mf:type rdfs:label "Type"@en, "Type"@nl ;
          rdf:value ?typeEn .

  mf:dateCreated  rdfs:label "Date"@en, "Datum"@nl ;
                  rdf:value ?dateCreatedString .

  mf:pid rdfs:label "PID"@en, "PID"@nl ;
         rdf:value ?pid .

  mf:requiredStatement rdfs:label "Attribution"@en, "Attributie"@nl ;
                       rdf:value ?copyrightEn, ?copyrightNl .

  ?browseThumbnailIri a dctypes:StillImage ;
                      dc:format "image/jpeg" .
  
  ?cp a dct:Agent ;
      rdfs:label ?cpLabel ;
      foaf:homepage ?cpHomepage ;
      foaf:logo ?cpLogo .

  ?cpHomepage a dctypes:Text ;
              dc:format "text/html" .

  ?cpLogo a dctypes:StillImage ;
          dc:format "image/png" .
}
WHERE {
  ?s rel:isr/premis:storedAt/premis:medium haCarId:glassplate ;
     dct:format ?type ;
     schema:license ?license ;
     schema:identifier ?pid ;
     schema:name ?title ;
     haObj:hasIIIFCopy ?iiifRepresentation ;
     schema:copyrightNotice ?copyright ;
     schema:maintainer ?cp ;
     schema:dateCreated ?dateCreated ;
     haObj:hasAccessCopy ?browseRepresentation ;
     haObj:hasMasterCopy ?masterRepresentation .

  ?browseRepresentation a haObj:DigitalRepresentation ;
                        rel:hss/haObj:isMezzanineCopyOf ?s ;
                        rel:inc ?browseFile .

  ?browseFile a premis:File ;
              schema:thumbnailUrl ?browseThumbnailIri .

  OPTIONAL{
    ?browseFile schema:height/schema:value ?browseHeight ;
                schema:width/schema:value ?browseWidth .
  }

  ?masterRepresentation a haObj:DigitalRepresentation ;
                        rel:inc ?masterFile .

  OPTIONAL{
    ?masterFile schema:height/schema:value ?masterHeight ;
                schema:width/schema:value ?masterWidth .
  }
              
     

  ?cp skos:prefLabel ?cpLabel ;
      foaf:homepage ?cpHomepage ;
      haOrg:hasLogo ?cpLogo .

  ?iiifRepresentation rel:inc/premis:storedAt/rdf:value ?iiifFileUrl .
  
  FILTER (?license IN (haLic:IIIF-PUBLIC, haLic:IIIF-RESTRICTED))

  #  IIIF structure IRIs
  BIND(IRI(CONCAT("https://iiif.meemoo.be/presentation/3/", ?pid, "/manifest.json")) AS ?manifestIri)
  BIND(IRI(CONCAT("https://iiif.meemoo.be/presentation/3/", ?pid, "/canvas/1")) as ?canvasIri)
  BIND(IRI(CONCAT("https://iiif.meemoo.be/presentation/3/", ?pid, "/canvas/1/page/1")) as ?annotationPageIri)
  BIND(IRI(CONCAT("https://iiif.meemoo.be/presentation/3/", ?pid, "/canvas/1/page/1/annotation/1")) as ?annotationIri)
  BIND(IRI(CONCAT(STR(?iiifFileUrl),".jph")) AS ?serviceIri)
  BIND(IRI(CONCAT(STR(?serviceIri), "/full/max/0/default.jpg")) AS ?bodyIri)

  #  Specific values
  BIND(IF(LANG(?title) = "nl", ?title, 1/0) AS ?titleNl)
  BIND(STRLANG(?copyright, "en") AS ?copyrightEn)
  BIND(STRLANG(?copyright, "nl") AS ?copyrightNl)
  BIND(STRLANG(?type, "en") AS ?typeEn)
  BIND(STR(?dateCreated) AS ?dateCreatedString)
  BIND(
    IF(BOUND(?browseHeight), ?browseHeight, 
      IF(BOUND(?masterHeight), ?masterHeight, 0)
    )
    AS ?fileHeight
  )
  BIND(
    IF(BOUND(?browseWidth), ?browseWidth, 
      IF(BOUND(?masterWidth), ?masterWidth, 0)
    )
    AS ?fileWidth
  )
  # BIND(
  #   COALESCE(
  #     IF(BOUND(?browseHeight), ?browseHeight, 1/0),
  #     IF(BOUND(?masterHeight), ?masterHeight, 1/0),
  #     0
  #   )
  #   AS ?fileHeight
  # )
  # BIND(
  #   COALESCE(
  #     IF(BOUND(?browseWidth), ?browseWidth, 1/0),
  #     IF(BOUND(?masterWidth), ?masterWidth, 1/0),
  #     0
  #   )
  #   AS ?fileWidth
  # )
}