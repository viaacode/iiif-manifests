prefix as: <http://www.w3.org/ns/activitystreams#>
prefix dc: <http://purl.org/dc/elements/1.1/>
prefix dct: <http://purl.org/dc/terms/>
prefix dctypes: <http://purl.org/dc/dcmitype/>
prefix foaf: <http://xmlns.com/foaf/0.1/>
prefix iiif_prezi: <http://iiif.io/api/presentation/3#>
prefix iiif_image_3: <http://iiif.io/api/image/3#>
prefix mf: <https://api.meemoo-int.triply.cc/queries/milan/iiif-glassplate-manifest/1/run#>
prefix oa: <http://www.w3.org/ns/oa#>
prefix org: <http://www.w3.org/ns/org#> 
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix prov: <http://www.w3.org/ns/prov#>
prefix schema: <https://schema.org/>
prefix haLic: <https://data.hetarchief.be/id/license/>
prefix mh: <https://data.hetarchief.be/ns/mediahaven/>
prefix haObj: <https://data.hetarchief.be/ns/object/>
prefix haOrg: <https://data.hetarchief.be/ns/organization/>
prefix rel: <http://id.loc.gov/vocabulary/preservation/relationshipSubType/>
prefix premis: <http://www.loc.gov/premis/rdf/v3/>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>


CONSTRUCT {
  ?manifestIri a iiif_prezi:Manifest;
                rdfs:label ?titleNl ;
                iiif_prezi:behavior iiif_prezi:individualsHint; 
                iiif_prezi:metadataEntries mf:type, mf:dateCreated, mf:pid ;
                iiif_prezi:thumbnail ?browseThumbnailIri ;
                iiif_prezi:requiredStatement mf:requiredStatement ;
                dct:rights "https://creativecommons.org/licenses/by/4.0/deed.nl" ;
                as:items ?canvasIri ;
                schema:provider ?cp .

  ?browseThumbnailIri a dctypes:StillImage ;
                      dc:format "image/jpeg" .

  ?cp a dct:Agent ;
      rdfs:label ?cpLabel ;
      foaf:homepage ?cpHomepage ;
      foaf:logo ?cpLogo .

  ?cpHomepage a dctypes:Text ;
              dc:format "text/html" .

  ?cpLogo a dctypes:StillImage ;
          dc:format "image/png" .
  
  mf:type rdfs:label "Type"@en, "Type"@nl ;
          rdf:value ?typeEn .

  mf:dateCreated  rdfs:label "Date"@en, "Datum"@nl ;
                  rdf:value ?dateCreatedString .

  mf:pid rdfs:label "PID"@en, "PID"@nl ;
         rdf:value ?pid .

  mf:requiredStatement rdfs:label "Attribution"@en, "Attributie"@nl ;
                       rdf:value ?copyrightEn, ?copyrightNl .

  ?canvasIri a iiif_prezi:Canvas ;
             iiif_prezi:thumbnail ?browseThumbnailIri ;
             rdfs:label ?titleNl ;
             as:items ?annotationPageIri .

  ?annotationPageIri a as:OrderedCollectionPage ;
                     as:items ?annotationIri .

  ?annotationIri a oa:Annotation ;
                oa:motivatedBy "painting" ;
                oa:hasTarget ?canvasIri ;
                oa:hasBody ?bodyIri .
  
  ?bodyIri a dctypes:StillImage ;
            dc:format "image/jpeg" ;
            schema:potentialAction ?serviceIri .

  ?serviceIri a iiif_image_3:ImageService ;
             dct:conformsTo "level2" .
}
WHERE {
  ?s prov:wasDerivedFrom ?mhRecord ;
     schema:license ?license ;
     schema:name ?title ;
     haObj:hasIIIFCopy ?iiifRepresentation ;
     schema:copyrightNotice ?copyright ;
     schema:identifier ?pid ;
     dct:format ?type ;
     schema:maintainer ?cp ;
     schema:dateCreated ?dateCreated ;
     haObj:hasAccessCopy ?browseRepresentation .

  ?browseRepresentation a haObj:DigitalRepresentation ;
                        rel:inc ?browseFile .

  ?browseFile a premis:File ;
              schema:thumbnailUrl ?browseThumbnailIri .
     

  ?cp org:identifier ?cpId ;
      skos:prefLabel ?cpLabel ;
      foaf:homepage ?cpHomepage ;
      haOrg:hasLogo ?cpLogo .
      
  ?mhRecord mh:fragmentIdentifier ?fragmentId .

  ?iiifRepresentation rel:inc ?iiifFile .

  ?iiifFile premis:storedAt ?iiifFileLocation .

  ?iiifFileLocation rdf:value ?iiifFileUrl .
  
  FILTER (?license IN (haLic:IIIF-PUBLIC))

  #  IIIF structure IRIs
  BIND(IRI(CONCAT("https://iiif.meemoo.be/presentation/3/", ?pid, "/manifest.json")) AS ?manifestIri)
  BIND(IRI(CONCAT("https://iiif.meemoo.be/presentation/3/", ?pid, "/canvas/1")) as ?canvasIri)
  BIND(IRI(CONCAT("https://iiif.meemoo.be/presentation/3/", ?pid, "/canvas/1/page/1")) as ?annotationPageIri)
  BIND(IRI(CONCAT("https://iiif.meemoo.be/presentation/3/", ?pid, "/canvas/1/page/1/annotation/1")) as ?annotationIri)
  BIND(IRI(CONCAT(STR(?iiifFileUrl),".jph")) AS ?serviceIri)
  BIND(IRI(CONCAT(STR(?serviceIri), "/full/max/0/default.jpg")) AS ?bodyIri)
  BIND(IRI(CONCAT(STR(?serviceIri),"/full/480,/0/default.jpg")) AS ?thumbnailIri)

  #  Specific values
  BIND(IF(LANG(?title) = "nl", ?title, 1/0) AS ?titleNl)
  BIND(STRLANG(?copyright, "en") AS ?copyrightEn)
  BIND(STRLANG(?copyright, "nl") AS ?copyrightNl)
  BIND(STRLANG(?type, "en") AS ?typeEn)
  BIND(STR(?dateCreated) AS ?dateCreatedString)
}